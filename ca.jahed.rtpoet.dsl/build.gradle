plugins {
	id 'java-library'
	id 'maven-publish'
}

dependencies {
	compile "org.eclipse.xtext:org.eclipse.xtext:${xtextVersion}"
	compile "org.eclipse.xtext:org.eclipse.xtext.xbase:${xtextVersion}"
}
configurations {
	mwe2 {
		extendsFrom compile
	}
}

dependencies {
	mwe2 'org.eclipse.emf:org.eclipse.emf.mwe2.launch'
	mwe2 "org.eclipse.xtext:org.eclipse.xtext.common.types:${xtextVersion}"
	mwe2 "org.eclipse.xtext:org.eclipse.xtext.xtext.generator:${xtextVersion}"
	mwe2 'org.eclipse.xtext:xtext-antlr-generator'
}

task generateXtextLanguage(type: JavaExec) {
	main = 'org.eclipse.emf.mwe2.launch.runtime.Mwe2Launcher'
	classpath = configurations.mwe2
	inputs.file "src/main/java/ca/jahed/rtpoet/dsl/GenerateRt.mwe2"
	inputs.file "src/main/java/ca/jahed/rtpoet/dsl/Rt.xtext"
	outputs.dir "src/main/xtext-gen"
	args += "src/main/java/ca/jahed/rtpoet/dsl/GenerateRt.mwe2"
	args += "-p"
	args += "rootPath=/${projectDir}/.."
}

generateXtext.dependsOn(generateXtextLanguage)
clean.dependsOn(cleanGenerateXtextLanguage)
eclipse.classpath.plusConfigurations += [configurations.mwe2]

publishing {
	repositories {
		mavenLocal()
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/qumase/rtpoet")
			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("GH_USER")
				password = project.findProperty("gpr.key") ?: System.getenv("GH_TOKEN")
			}
		}
	}

	publications {
		mvn(MavenPublication) {
			from components.java
			groupId = "ca.jahed.rtpoet"
			artifactId = "rtpoet-dsl"
			versionMapping {
				usage('java-api') {
					fromResolutionOf('runtimeClasspath')
				}
				usage('java-runtime') {
					fromResolutionResult()
				}
			}
			pom {
				name = 'RTPoet DSL'
				description = 'Xtext DSL for RTPoet'
				url = 'https://github.com/qumase/rtpoet-dsl'
				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
				developers {
					developer {
						id = 'kjahed'
						name = 'Karim Jahed'
						email = 'jahed@cs.queensu.ca'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/qumase/rtpoet-dsl.git'
					developerConnection = 'scm:git:ssh://github.com/qumase/rtpoet-dsl.git'
					url = 'https://github.com/qumase/rtpoet-dsl/'
				}
			}
		}
	}
}